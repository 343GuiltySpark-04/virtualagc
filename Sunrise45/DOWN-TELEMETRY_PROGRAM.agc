### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	DOWN-TELEMETRY_PROGRAM.agc
## Purpose:	Part of the source code for Solarium build 55. This
##		is for the Command Module's (CM) Apollo Guidance
##		Computer (AGC), for Apollo 6.
## Assembler:	yaYUL --block1
## Contact:	Jim Lawton <jim DOT lawton AT gmail DOT com>
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2009-09-29 JL	Created.
##		2016-08-19 RSB	Typo.
## 		2016-12-28 RSB	Proofed comment text using octopus/ProoferComments,
##				and fixed errors found.


# TELEMETRY PROCESSOR
# --------- ---------
#
#	THE FOLLOWING TELEMETRY PROGRAM IS DESIGNED TO TRANSMIT TELEMETRY DATA VIA OUT4 WHEN AN ENDPULSE 
# FROM THE NORTH AMERICAN TELEMETRY PROGRAMMER TRIGGERS INTERRUPT 6, WHICH INITIATES THIS ROUTINE. IT OPERATES
# IN CONJUNCTION WITH (BUT ASYNCHRONOUSLY FROM) THE T4RUPT PROGRAM, WHICH IS INITATED EVERY 60 MS, VIA INTERRUPT
# 3. 


		BANK	1
DOWNRUPT	CCS	TELCOUNT	# PNZ IS NORMAL SETTING - +0 INDICATES
		TC	DOWNTMOK	# TM FAILURE SINCE ENDPULSES ARE OCCURING
		TC	TMFAIL		# TOO FREQUENTLY. THE COUNTER IS SET TO +7
		
		CS	BIT10		# BLOCK TM ENDPULSES UNTIL  ERROR RESET
		MASK	OUT1		# COMMAND IS GIVEN.
		AD	BIT10
		TS	OUT1
		
		TC	NBRESUME	# BY DSRUPT EVERY 120 MS.
		
DOWNTMOK	TS	TELCOUNT	# NORMAL MODE - STORE DECREMENTED COUNT.

DWNRELAY	CCS	DISPBUF
		TC	+2
		TC	DWNKEY

		CAF	ZERO
		XCH	DISPBUF

DATADWNF	TS	OUT4
		CS	BIT9
		MASK	OUT1
		TS	OUT1
		TC	NBRESUME	# NO BANK SWITCH REQUIRED.

DWNKEY		CCS	TMKEYBUF
		TC	+2
		TC	DWNID

		CAF	ZERO
		XCH	TMKEYBUF
		TC	DATADWNF

DATADWN		CCS	IDPLACER
		TC	+2
		TC	DWNID

		TS	IDPLACER
		CCS	TMINDEX
		TC	+2
		CAF	DNLST1SZ
		TS	TMINDEX

		INDEX	A
		INDEX	DNLST1
		CS	0
		COM
		TS	OUT4
		CS	BIT9
		MASK	OUT1
		AD	BIT9
		TS	OUT1
		TC	NBRESUME

DWNID		AD	TMINDEX
		TS	OUT4
		CAF	FOUR
		TS	IDPLACER
		TC	DATADWNF +1

#	SUBROUTINE COMMON TO UPLINK AND DOWNLINK TO TURN ON THE TM FAIL LIGHT.

TMFAIL		CS	BIT4
		MASK	OUT1
		AD	BIT4
		TS	OUT1
		TC	Q

DNLST1SZ	DEC	31

DNLST1		ADRES	TIME1
		ADRES	TIME2
		ADRES	IN0
		ADRES	IN2
		ADRES	IN3
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
		ADRES	OUT1
